plugins {
    id "application"
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.th2.component)
    alias(libs.plugins.jmh)
}

group = 'com.exactpro.th2'
version = release_version

ext.excludeSailfish = { rcd ->
    rcd.excludeModule("com.exactpro.sf", "sailfish-core")
    rcd.excludeModule("com.exactpro.sf", "sailfish-common")
    rcd.excludeModule("com.exactpro.sf", "sailfish-rest-api-client")
    rcd.excludeModule("com.exactpro.sf", "service-http")
}

repositories {
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        content {
            excludeSailfish(it)
        }
    }

    maven { // FIXME: remove before merge into master
        name 'Sonatype_snapshot'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        content {
            excludeSailfish(it)
        }
    }

    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }

    maven { // FIXME: remove before merge into master
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }

    mavenCentral()
    mavenLocal()

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    implementation(libs.th2.common)
    implementation(libs.th2.codec)

    implementation (libs.sailfish.common) {
        exclude group: "commons-configuration", module: "commons-configuration" // because of the vulnerability
        exclude group: "com.fasterxml.jackson.dataformat", module: "jackson-dataformat-yaml" // because of the vulnerability
        exclude group: "com.exactpro.mina", module: "apache-mina-core" // because of the vulnerability
        exclude group: "javax.activation", module: "javax.activation-api" // because of the license
    }

    implementation "io.netty:netty-buffer"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation(libs.kotlin.logging)

    compileOnly(libs.auto.service)
    kapt(libs.auto.service)

    jmh(libs.jmh.core)
    jmh(libs.jmh.generator.annprocess)
    jmh(libs.jmh.generator.bytecode)

    testImplementation(libs.assertj.core)
    testImplementation(platform(libs.junit.bom))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.junit.platform:junit-platform-launcher") {
        because 'This artifact will not be added automatically since gradle 9.0. You can read more information here:' +
                'https://docs.gradle.org/8.3/userguide/upgrading_version_8.html#test_framework_implementation_dependencies'
    }
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
}

test.useJUnitPlatform()
application.mainClass = "com.exactpro.th2.codec.MainKt"
dependencyCheck.suppressionFile = "suppressions.xml"